set(TARGET_NAME vircadia-sdk)

find_package(Git QUIET REQUIRED)

execute_process(
    COMMAND "${GIT_EXECUTABLE}" rev-parse --short HEAD
    WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}"
    OUTPUT_VARIABLE VIRCADIA_SDK_GIT_COMMIT_SHA_SHORT
    ERROR_QUIET
    OUTPUT_STRIP_TRAILING_WHITESPACE)

execute_process(
    COMMAND "${GIT_EXECUTABLE}" rev-parse --show-toplevel
    WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}"
    OUTPUT_VARIABLE VIRCADIA_SDK_GIT_ROOT
    ERROR_QUIET
    OUTPUT_STRIP_TRAILING_WHITESPACE)

set(VIRCADIA_SDK_VERSION_MAJOR 0)
set(VIRCADIA_SDK_VERSION_MINOR 0)
set(VIRCADIA_SDK_VERSION_TWEAK 1)
set(VIRCADIA_SDK_VERSION "v${VIRCADIA_SDK_VERSION_MAJOR}.${VIRCADIA_SDK_VERSION_MINOR}.${VIRCADIA_SDK_VERSION_TWEAK}")
set(VIRCADIA_SDK_VERSION_FULL "${VIRCADIA_SDK_VERSION}-git.${VIRCADIA_SDK_GIT_COMMIT_SHA_SHORT}")

add_compile_definitions(VIRCADIA_SDK_GIT_COMMIT_SHA_SHORT=${VIRCADIA_SDK_GIT_COMMIT_SHA_SHORT})
add_compile_definitions(VIRCADIA_SDK_VERSION_MAJOR=${VIRCADIA_SDK_VERSION_MAJOR})
add_compile_definitions(VIRCADIA_SDK_VERSION_MINOR=${VIRCADIA_SDK_VERSION_MINOR})
add_compile_definitions(VIRCADIA_SDK_VERSION_TWEAK=${VIRCADIA_SDK_VERSION_TWEAK})
add_compile_definitions(VIRCADIA_SDK_VERSION=${VIRCADIA_SDK_VERSION})
add_compile_definitions(VIRCADIA_SDK_VERSION_FULL=${VIRCADIA_SDK_VERSION_FULL})
add_compile_definitions(VIRCADIA_SDK_DLL_EXPORT)

set_property(GLOBAL APPEND
    PROPERTY CMAKE_CONFIGURE_DEPENDS
    "${VIRCADIA_SDK_GIT_ROOT}/.git/index")

# check if Doxygen is installed
find_package(Doxygen)
if (DOXYGEN_FOUND)
    set(DOXYGEN_PROJECT_NAME "Vircadia SDK")
    set(DOXYGEN_PROJECT_VERSION ${VIRCADIA_SDK_VERSION_FULL})
    set(DOXYGEN_GENERATE_MAN YES)
    doxygen_add_docs(
        vircadia-sdk-docs
        COMMENT "Generate Vircadia SDK documentation."
    )
    unset(DOXYGEN_PROJECT_NAME)
    unset(DOXYGEN_PROJECT_VERSION)
    unset(DOXYGEN_GENERATE_MAN)
else (DOXYGEN_FOUND)
    message("Doxygen not found. Vircadia SDK documentation target will not be generatred.")
endif (DOXYGEN_FOUND)

setup_hifi_library()
link_hifi_libraries(shared networking)
