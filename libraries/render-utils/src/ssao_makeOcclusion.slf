<@include gpu/Config.slh@>
<$VERSION_HEADER$>
//  Generated on <$_SCRIBE_DATE$>
//
//  ssao_makeOcclusion.frag
//
//  Created by Sam Gateau on 1/1/16.
//  Copyright 2016 High Fidelity, Inc.
//
//  Distributed under the Apache License, Version 2.0.
//  See the accompanying file LICENSE or http://www.apache.org/licenses/LICENSE-2.0.html
//

<@include ssao.slh@>
<$declareAmbientOcclusion()$>
<$declareFetchDepthPyramidMap()$>
<$declareSamplingDisk()$>
<$declareEvalObscurance()$>

<$declarePackOcclusionDepth()$>

#define SSAO_USE_HORIZON_BASED	1
#define SSAO_HBAO_MAX_RADIUS    100.0

layout(location=0) out vec4 outFragColor;

void main(void) {
    vec2 sideImageSize = getSideImageSize(getResolutionLevel());

    // Pixel being shaded
    vec2 fragCoord = gl_FragCoord.xy; 
    ivec2 shadedPixelPos = ivec2(fragCoord.xy);

    // Stereo side info
    ivec4 side = getStereoSideInfo(shadedPixelPos.x, getResolutionLevel());

    // From now on, shadedPixelPos is the pixel pos in the side
    shadedPixelPos.x -= side.y;
    vec2 shadedUVPos = (vec2(shadedPixelPos) + vec2(0.5))  / sideImageSize;

    // Fetch the z under the pixel (stereo or not)
    float Zeye = getZEyeAtUV(shadedUVPos, 0);

    // The position  and normal of the pixel fragment in Eye space
    vec3 fragPositionES = evalEyePositionFromZeye(side.x, Zeye, shadedUVPos);
    vec3 fragNormalES = evalEyeNormal(fragPositionES);

    // Choose the screen-space sample radius
    float diskPixelRadius = evalDiskRadius(fragPositionES.z, sideImageSize);
#if SSAO_USE_HORIZON_BASED
    diskPixelRadius = min(diskPixelRadius, SSAO_HBAO_MAX_RADIUS);
#endif

    // Let's make noise 
    float randomPatternRotationAngle = getAngleDithering(shadedPixelPos);

    // Accumulate the obscurance for each samples
    float obscuranceSum = 0.0;
    int numSamples = int(getNumSamples());
    for (int i = 0; i < numSamples; ++i) {
#if SSAO_USE_HORIZON_BASED
		vec3 deltaTap = getUnitTapLocation(i, 1.0, randomPatternRotationAngle, PI);
        obscuranceSum += evalVisibilityHBAO(side, shadedPixelPos, sideImageSize, deltaTap.xy, diskPixelRadius, fragPositionES, fragNormalES);
#else
        vec3 tap = getTapLocationClampedSSAO(i, randomPatternRotationAngle, diskPixelRadius, shadedPixelPos, sideImageSize);
        vec3 tapUVZ = fetchTap(side, shadedPixelPos, tap, sideImageSize);
        vec3 tapPositionES = evalEyePositionFromZeye(side.x, tapUVZ.z, tapUVZ.xy);
        obscuranceSum += float(tap.z > 0.0) * evalVisibilitySSAO(fragPositionES, fragNormalES, tapPositionES);
#endif
    }

    float occlusion = clamp(1.0 - obscuranceSum * getObscuranceScaling() * getInvNumSamples(), 0.0, 1.0);

     // KEEP IT for Debugging
    // Bilateral box-filter over a quad for free, respecting depth edges
    // (the difference that this makes is subtle)
/*    if (abs(dFdx(fragPositionES.z)) < 0.02) {
        occlusion -= dFdx(occlusion) * ((shadedPixelPos.x & 1) - 0.5);
    }
    if (abs(dFdy(fragPositionES.z)) < 0.02) {
        occlusion -= dFdy(occlusion) * ((shadedPixelPos.y & 1) - 0.5);
    }*/
    
    //outFragColor = vec4(packOcclusionDepth(occlusion, CSZToDepthKey(fragPositionES.z)), 1.0);
    outFragColor = vec4(vec3(occlusion), 1.0);

}
