<@include gpu/Config.slh@>
<$VERSION_HEADER$>
//  Generated on <$_SCRIBE_DATE$>
//
//  taa.frag
//  fragment shader
//
//  Created by Sam Gateau on 8/14/2017
//  Copyright 2017 High Fidelity, Inc.
//
//  Distributed under the Apache License, Version 2.0.
//  See the accompanying file LICENSE or http://www.apache.org/licenses/LICENSE-2.0.html
//


<@include taa.slh@>

in vec2 varTexCoord0;
layout(location = 0) out vec4 outFragColor;

void main() {
    vec2 fragUV = varTexCoord0;
    


    vec2 texelSize = getInvWidthHeight();
    vec2 fragJitterPix = taa_getCurrentJitterSample();
    if (taa_unjitter()) {
        fragUV -= fragJitterPix * texelSize;
    }

    // Debug region before debug or fxaa region X
    float distToRegionFXAA = fragUV.x - taa_getRegionFXAA().x;  
    if (distToRegionFXAA > 0.0) {
        outFragColor = vec4(taa_evalTXAA(fragUV), 1.0);
        return;
    }

    vec3 nearFragUV = taa_findClosestFragment3x3(fragUV);
    vec2 fragVel = taa_fetchVelocityMap(nearFragUV.xy);

    vec3 sourceColor;
    vec3 historyColor;
    vec2 prevFragUV = taa_fetchSourceAndHistory(fragUV, fragVel, sourceColor, historyColor);

    vec3 nextColor = mix(historyColor, sourceColor, params.blend);
 
     if (taa_constrainColor()) {
        mat3 colorMinMaxAvg = taa_evalNeighbourColorRegion(sourceColor, fragUV, fragVel, nearFragUV.z, fragJitterPix);
     
	    // clamp history to neighbourhood of current sample
        historyColor = taa_clampColor(colorMinMaxAvg[0], colorMinMaxAvg[1], colorMinMaxAvg[2], historyColor);
        nextColor = historyColor;
    }
    
    if (taa_feedbackColor()) {
         nextColor = taa_evalFeedbackColor(sourceColor, historyColor, params.blend);
    }

    outFragColor = vec4(taa_resolveColor(nextColor), 1.0);
}
