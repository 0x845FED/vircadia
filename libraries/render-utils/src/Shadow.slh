<!
//  Shadow.slh
//  libraries/render-utils/src
//
//  Created by Sam Gateau on 1/4/15.
//  Copyright 2013 High Fidelity, Inc.
//
//  Distributed under the Apache License, Version 2.0.
//  See the accompanying file LICENSE or http://www.apache.org/licenses/LICENSE-2.0.html
!>
<@if not SHADOW_SLH@>
<@def SHADOW_SLH@>

// the shadow texture
uniform sampler2DShadow shadowMap;

struct ShadowTransform {
	mat4 reprojection;

	float bias;
	float scale;
};

uniform shadowTransformBuffer {
	ShadowTransform _shadowTransform;
};

mat4 getShadowReprojection() {
	return _shadowTransform.reprojection;
}

float getShadowScale() {
	return _shadowTransform.scale;
}

float getShadowBias() {
	return _shadowTransform.bias;
}

// Compute the texture coordinates from world coordinates
vec4 evalShadowTexcoord(vec4 position) {
	float bias = -getShadowBias();

	vec4 shadowCoord = getShadowReprojection() * position;
	return vec4(shadowCoord.xy, shadowCoord.z + bias, 1.0);
}

// Sample the shadowMap with PCF (built-in)
float fetchShadow(vec3 shadowTexcoord) {
    return texture(shadowMap, shadowTexcoord);
}

vec2 PCFkernel[4] = vec2[4](
    vec2(-1.5, 0.5),
    vec2(0.5, 0.5),
    vec2(-1.5, -1.5),
    vec2(0.5, -1.5)
);

float evalShadowNoise(vec4 seed) {
    float dot_product = dot(seed, vec4(12.9898,78.233,45.164,94.673));
    return fract(sin(dot_product) * 43758.5453);
}

float evalShadowAttenuationPCF(vec4 position, vec4 shadowTexcoord) {
	float shadowScale = getShadowScale();

#if 0
    // Pattern dithering in screen space
    ivec2 coords = ivec2(gl_FragCoord.xy);
#else
    // Pattern dithering in world space (mm resolution)
    ivec2 coords = ivec2(position.x, position.y+position.z);
#endif
    // Add some noise to break dithering
    int index = int(4.0*evalShadowNoise(gl_FragCoord.xyyx))%4;
    coords.x += index & 1;
    coords.y += (index & 2) >> 1;

    // Offset for efficient PCF, see http://http.developer.nvidia.com/GPUGems/gpugems_ch11.html
    ivec2 offset = coords & ivec2(1,1);
    offset.y = (offset.x+offset.y) & 1;
    vec2 offsetF = vec2(offset);

    float shadowAttenuation = (0.25 * (
        fetchShadow(shadowTexcoord.xyz + shadowScale * vec3(offsetF + PCFkernel[0], 0.0)) +
        fetchShadow(shadowTexcoord.xyz + shadowScale * vec3(offsetF + PCFkernel[1], 0.0)) +
        fetchShadow(shadowTexcoord.xyz + shadowScale * vec3(offsetF + PCFkernel[2], 0.0)) +
        fetchShadow(shadowTexcoord.xyz + shadowScale * vec3(offsetF + PCFkernel[3], 0.0))
    ));

    return shadowAttenuation;
}

float evalShadowAttenuation(vec4 position) {
	vec4 shadowTexcoord = evalShadowTexcoord(position);
    if (shadowTexcoord.x < 0.0 || shadowTexcoord.x > 1.0 ||
        shadowTexcoord.y < 0.0 || shadowTexcoord.y > 1.0 ||
        shadowTexcoord.z < 0.0 || shadowTexcoord.z > 1.0) {
        // If a point is not in the map, do not attenuate
        return 1.0;
    }

    return evalShadowAttenuationPCF(position, shadowTexcoord);
}

<@endif@>
