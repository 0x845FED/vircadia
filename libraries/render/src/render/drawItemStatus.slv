<@include gpu/Config.slh@>
<$VERSION_HEADER$>
//  Generated on <$_SCRIBE_DATE$>
//
//  drawItemStatus.slv
//  vertex shader
//
//  Created by Sam Gateau on 6/30/2015.
//  Copyright 2015 High Fidelity, Inc.
//
//  Distributed under the Apache License, Version 2.0.
//  See the accompanying file LICENSE or http://www.apache.org/licenses/LICENSE-2.0.html
//

<@include gpu/Transform.slh@>

<$declareStandardTransform()$>

varying vec4 varColor;

attribute vec4 inStatus;

vec3 paintRainbow(float nv) {
    float v = nv * 5.f;
    if ( v < 0.f )
        return vec3(1.f, 0.f, 0.f);
    else if ( v < 1.f)
        return vec3(1.f, v, 0.f);
    else if ( v < 2.f)
        return vec3(1.f - (v-1.f), 1.f, 0.f);
    else if ( v < 3.f)
        return vec3(0.f, 1.f, (v-2.f));
    else if ( v < 4.f)
        return vec3(0.f, 1.f - (v-3.f), 1.f );
    else if ( v < 5.f)
        return vec3((v-4.f), 0.f, 1.f );
    else 
        return vec3(1.f, 0.f, 1.f);
}

void main(void) {
    const vec2 ICON_PIXEL_SIZE = vec2(10, 10);
    const vec4 UNIT_QUAD[4] = vec4[4](
        vec4(-1.0, -1.0, 0.0, 1.0),
        vec4(1.0, -1.0, 0.0, 1.0),
        vec4(-1.0, 1.0, 0.0, 1.0),
        vec4(1.0, 1.0, 0.0, 1.0)
    );

    // Use the status for showing a color ?
    varColor = vec4(paintRainbow(inStatus.x), 1.0);


    vec4 anchorPoint = vec4(0.5, 0.5, 0.5, 1.0);
    TransformCamera cam = getTransformCamera();
    TransformObject obj = getTransformObject();
    <$transformModelToClipPos(cam, obj, anchorPoint, anchorPoint)$>

    vec4 pos = UNIT_QUAD[gl_VertexID];

    vec4 viewport;
    <$transformCameraViewport(cam, viewport)$>;
    vec2 clipIconSize = vec2(ICON_PIXEL_SIZE.x / viewport.z, ICON_PIXEL_SIZE.y / viewport.w);

    gl_Position = anchorPoint + anchorPoint.w * vec4(pos.xy * clipIconSize, 0.0, 0.0);
}