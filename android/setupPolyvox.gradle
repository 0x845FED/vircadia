buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'de.undercouch:gradle-download-task:3.3.0'
    }
}

def file='polyvox_armv8-libcpp.tgz'
def url='https://hifi-public.s3.amazonaws.com/austin/android/' + file
def destFile = new File(HIFI_ANDROID_PRECOMPILED, file)

task downloadPolyvox(type: de.undercouch.gradle.tasks.download.Download) {
    src url
    dest destFile
}

task extractPolyvox(dependsOn: downloadPolyvox, type: Copy) {
    from tarTree(resources.gzip(destFile))
    into new File(HIFI_ANDROID_PRECOMPILED, 'polyvox')
}

task copyPolyvoxCore(dependsOn: extractPolyvox, type: Copy) {
    from HIFI_ANDROID_PRECOMPILED + '/polyvox/lib/Release'
    include 'libPolyVoxCore.so'
    into HIFI_ANDROID_PRECOMPILED + '/jni/arm64-v8a'
}

task copyPolyvoxUtil(dependsOn: extractPolyvox, type: Copy) {
    from HIFI_ANDROID_PRECOMPILED + '/polyvox/lib'
    include 'libPolyVoxUtil.so'
    into HIFI_ANDROID_PRECOMPILED + '/jni/arm64-v8a'
}

task setupPolyvox(dependsOn: [copyPolyvoxCore, copyPolyvoxUtil]) { }
